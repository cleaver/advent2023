<!-- livebook:{"file_entries":[{"file":{"file_system_id":"local","file_system_type":"local","path":"/Users/cleaver/Documents/projects/elixir/advent2023/day02.livemd"},"name":"day02.livemd","type":"file"}]} -->

# Day 1

```elixir
Mix.install([
  {:kino, "~> 0.11.3"}
])
```

[![Run in Livebook](https://livebook.dev/badge/v1/blue.svg)](https://livebook.dev/run?url=https%3A%2F%2Fgithub.com%2Fcleaver%2Fadvent2023%2Fblob%2Fmain%2Fday01.livemd)

## Data

```elixir
input = Kino.Input.textarea("Please paste your input file:")
```

## Solutions

### Part 1

Something is wrong with global snow production, and you've been selected to take a look. The Elves have even given you a map; on it, they've used stars to mark the top fifty locations that are likely to be having problems.

You've been doing this long enough to know that to restore snow operations, you need to check all fifty stars by December 25th.

Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!

You try to ask why they can't just use a weather machine ("not powerful enough") and where they're even sending you ("the sky") and why your map looks mostly blank ("you sure ask a lot of questions") and hang on did you just say the sky ("of course, where do you think snow comes from") when you realize that the Elves are already loading you into a trebuchet ("please hold still, we need to strap you in").

As they're making the final adjustments, they discover that their calibration document (your puzzle input) has been amended by a very young Elf who was apparently just excited to show off her art skills. Consequently, the Elves are having trouble reading the values on the document.

The newly-improved calibration document consists of lines of text; each line originally contained a specific calibration value that the Elves now need to recover. On each line, the calibration value can be found by combining the first digit and the last digit (in that order) to form a single two-digit number.

For example:

```
1abc2
pqr3stu8vwx
a1b2c3d4e5f
treb7uchet
```

In this example, the calibration values of these four lines are 12, 38, 15, and 77. Adding these together produces 142.

Consider your entire calibration document. What is the sum of all of the calibration values?

```elixir
input = Kino.Input.read(input)

defmodule Advent2023.Day01.Part1 do
  @text_digits Enum.map(0..9, &{Integer.to_string(&1), &1}) |> Enum.into(%{})

  def get_first_and_last_digit(text) do
    text
    |> String.split("", trim: true)
    |> Enum.reduce({nil, 0}, &process_character/2)
  end

  def process_character(character, {nil, last}) do
    maybe_digit = @text_digits[character]
    if maybe_digit, do: {maybe_digit, maybe_digit}, else: {nil, last}
  end

  def process_character(character, {first, last}) do
    maybe_digit = @text_digits[character]
    if maybe_digit, do: {first, maybe_digit}, else: {first, last}
  end

  def add({nil, last}), do: last
  def add({first, last}), do: first * 10 + last

  def single_calibration(calibration) do
    calibration
    |> get_first_and_last_digit()
    |> add()
  end

  def multiple_calibrations(calibration_list) do
    calibration_list
    |> Enum.reduce(0, fn sum, acc -> single_calibration(sum) + acc end)
  end
end

input
|> String.splitter("\n", trim: true)
|> Advent2023.Day01.Part1.multiple_calibrations()
```

### Part 2

Your calculation isn't quite right. It looks like some of the digits are actually spelled out with letters: one, two, three, four, five, six, seven, eight, and nine also count as valid "digits".

Equipped with this new information, you now need to find the real first and last digit on each line. For example:

```
two1nine
eightwothree
abcone2threexyz
xtwone3four
4nineeightseven2
zoneight234
7pqrstsixteen
```

In this example, the calibration values are 29, 83, 13, 24, 42, 14, and 76. Adding these together produces 281.

**What is the sum of all of the calibration values?**

```elixir
defmodule Advent2023.Day01.Part2 do
  @number_lookup %{
    "one" => "1",
    "two" => "2",
    "three" => "3",
    "four" => "4",
    "five" => "5",
    "six" => "6",
    "seven" => "7",
    "eight" => "8",
    "nine" => "9",
    "zero" => "0"
  }
  @text_digits Enum.map(0..9, &{Integer.to_string(&1), &1}) |> Enum.into(%{})

  def get_matched_numbers(text) do
    list_of_all_digits = Map.keys(@number_lookup) ++ Map.values(@number_lookup)
    pattern = ~r/(?=(#{Enum.join(list_of_all_digits, "|")}))/i
    Regex.scan(pattern, text)
  end

  def get_first_and_last_digit([]), do: {0, 0}

  def get_first_and_last_digit(list) do
    [_, first] = List.first(list)
    [_, last] = List.last(list)
    {numberize(first), numberize(last)}
  end

  def numberize(number_string) do
    Map.get(@number_lookup, number_string, number_string)
    |> String.to_integer()
  end

  def add({first, last}), do: first * 10 + last

  def single_calibration(calibration) do
    calibration
    |> get_matched_numbers()
    |> get_first_and_last_digit()
    |> add()
  end

  def multiple_calibrations(calibration_list) do
    calibration_list
    |> Enum.reduce(0, fn sum, acc -> single_calibration(sum) + acc end)
  end
end

# Advent2023.Day01.Part2.single_calibration("eightwothree")

input
|> String.splitter("\n", trim: true)
|> Advent2023.Day01.Part2.multiple_calibrations()
```

<!-- livebook:{"offset":5283,"stamp":{"token":"XCP.YkS2a-BNc5cd2SmhG0_WSgk9dnKDNeCemfIS3Ym8JkLTP5BwikODFBseUSHX2Tu5RsHtmgpnij0SifvZkVNXttRjtCcDy_1NTyZVTw","version":2}} -->
